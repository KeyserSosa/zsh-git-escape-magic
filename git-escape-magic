#
# git-escape-magic - zle tweak for git command line arguments
#
# Copyright (c) 2011 Akinori MUSHA
# Licensed under the 2-clause BSD license.
#
# This tweak eliminates the need for manually escaping shell
# meta-characters such as [~^{}] that are used for specifying a git
# object (commit or tree).  Every time you type one of these
# characters on a git command line, it is automatically escaped with a
# backslash as necessary and as appropriate.
#
# If you want to use this with url-quote-magic, make sure to enable it
# first.
#
# Usage:
#     autoload -Uz git-escape-magic
#     git-escape-magic
#

git-escape-magic.self-insert() {
    setopt localoptions noksharrays extendedglob
    local self_insert_function
    zstyle -s ':git-escape-magic' self-insert-function self_insert_function

    local qkey="${(q)KEYS}"
    if [[ "$KEYS" != "$qkey" ]]
    then
        local lbuf="$LBUFFER$qkey"
        if [[ "${(Q)LBUFFER}$KEYS" == "${(Q)lbuf}" ]]
        then
            local -a words
            words=("${(@Q)${(z)lbuf}}")

            [[ "$words[1]" == (*/|)git(|-[^/]##) \
            && ${#words} -ge 3 \
            && ${#words} -eq ${#words:#--} \
            ]] &&
            case "${words[-1]%%"$KEYS"}" in
                (*@)
                    [[ "$KEYS" == *\{* ]] ;;
                (*@\{?*)
                    [[ "$KEYS" == *\}* ]] ;;
                (?*)
                    [[ "$KEYS" == *[~^]* ]] ;;
                (*)
                    false ;;
            esac &&
                LBUFFER="$LBUFFER\\"
        fi
    fi

    zle "$self_insert_function"
}

git-escape-magic.on() {
    local self_insert_function="${$(zle -lL | awk \
        '$1=="zle"&&$2=="-N"&&$3=="self-insert"{print $4;exit}'):-.self-insert}"

    [[ "$self_insert_function" == git-escape-magic.self-insert ]] &&
        return 0

    # For url-quote-magic which does not zle -N itself
    zle -la "$self_insert_function" || zle -N "$self_insert_function"

    zstyle ':git-escape-magic' self-insert-function "$self_insert_function"

    zle -A git-escape-magic.self-insert self-insert
    return 0
}

git-escape-magic.off() {
    local self_insert_function
    zstyle -s ':git-escape-magic' self-insert-function self_insert_function

    [[ -n "$self_insert_function" ]] &&
        zle -A "$self_insert_function" self-insert
    return 0
}

zle -N git-escape-magic.self-insert
zle -N git-escape-magic.on
zle -N git-escape-magic.off

git-escape-magic() {
        git-escape-magic.on
}

[[ -o kshautoload ]] || git-escape-magic "$@"
